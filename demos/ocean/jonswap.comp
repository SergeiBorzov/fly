#version 460
#extension GL_GOOGLE_include_directive : enable
#include "bindless.glsl"

#define G (9.81f)
#define PI (3.14159265f)

layout(local_size_x = 256) in;

layout(push_constant) uniform PushConstants
{
    uint uniformBufferIndex;
    uint dataBufferIndex;
}
gPushConstants;

FLY_REGISTER_UNIFORM_BUFFER(UniformData, {
    vec4 fetchSpeedDirSpread;
    vec4 timeScale;
    vec4 domainMinMax;
})

FLY_REGISTER_STORAGE_BUFFER(writeonly, Data, {
    vec2 value;
    vec2 displacement;
    vec2 dx;
    vec2 dy;
    vec2 dxDisplacement;
    vec2 dyDisplacement;
})

float Hash(vec2 n, float seed)
{
    return fract(sin(dot(n + seed, vec2(12.9898, 78.233))) * 43758.5453);
}

// Box-Muller transform using two hashed values
float GaussianHash(vec2 n, float seed)
{
    float u1 = Hash(n, seed);
    float u2 = Hash(n, seed + 1.0);
    float r = sqrt(-2.0 * log(max(u1, 1e-6)));
    float theta = 2.0 * PI * u2;
    return r * cos(theta);
}

float Jonswap(float w, float fetch, float windSpeed)
{
    const float y = 3.3;
    const float wp = 22.0f * pow((windSpeed * fetch) / (G * G), -0.33f);
    const float sigma = w > wp ? 0.09f : 0.07f;
    const float alpha =
        0.076f * pow((G * fetch) / (windSpeed * windSpeed), -0.22f);
    const float r = exp(-((w - wp) * (w - wp)) / (2 * sigma * sigma * wp * wp));

    return ((alpha * G * G) / pow(w, 5.0f)) *
           exp(-(5.0f / 4.0f) * pow(wp / w, 4.0f)) * pow(y, r);
}

// Quartic approx for normalization factor for cosine-2s spread function
float NormalizationFactor(float s)
{
    if (s < 5)
    {
        return (((-0.000564f * s + 0.00776f) * s - 0.044f) * s + 0.192f) * s +
               0.163f;
    }
    else
    {
        return ((((-4.80e-8f) * s + 1.07e-5f) * s - 9.53e-4f) * s + 0.059f) *
                   s +
               0.393f;
    }
}

float Cosine2s(float theta, float theta0, float s)
{
    float delta = 0.5f * (theta - theta0);
    return NormalizationFactor(s) * pow(abs(cos(delta)), 2 * s);
}

float WaveDispersion(float kl) { return sqrt(G * kl); }

float WaveDispersionDerivative(float kl) { return G / (2 * sqrt(G * kl)); }

float Spectrum(float kl, float fetch, float windSpeed, float theta,
               float theta0, float spread)
{
    return Jonswap(WaveDispersion(kl), fetch, windSpeed) *
           Cosine2s(theta, theta0, spread) * WaveDispersionDerivative(kl) / kl;
}

vec2 H0(vec2 k, float kl, float deltaK, float fetch, float windSpeed,
        float theta, float theta0, float spread)
{
    vec2 rand = vec2(GaussianHash(k, 0.0f), GaussianHash(k, 1.0f));

    return rand / sqrt(2) *
           sqrt(2 * Spectrum(kl, fetch, windSpeed, theta, theta0, spread) *
                deltaK * deltaK);
}

vec2 MultiplyComplex(vec2 a, vec2 b)
{
    return vec2(a.x * b.x - a.y * b.y, a.y * b.x + a.x * b.y);
}

void main()
{
    int c = int(gl_LocalInvocationID.x);
    int r = int(gl_WorkGroupID.x);
    int base = r * int(gl_WorkGroupSize.x);
    int size = int(gl_WorkGroupSize.x);

    vec4 fetchSpeedDirSpread = FLY_ACCESS_UNIFORM_BUFFER(
        UniformData, gPushConstants.uniformBufferIndex, fetchSpeedDirSpread);
    vec4 timeScale = FLY_ACCESS_UNIFORM_BUFFER(
        UniformData, gPushConstants.uniformBufferIndex, timeScale);
    vec4 domainMinMax = FLY_ACCESS_UNIFORM_BUFFER(
        UniformData, gPushConstants.uniformBufferIndex, domainMinMax);

    float deltaK = 2 * PI / domainMinMax.x;
    int halfSize = size / 2;
    float kx = (c < halfSize) ? c : c - size;
    float ky = (r < halfSize) ? r : r - size;
    vec2 k = vec2(kx, ky) * deltaK;
    float theta = atan(k.y, k.x);
    if (length(k) < 1e-4)
    {
        k.x = 0.0001;
        k.y = 0.0001;
    }
    float kl = length(k);

    float spectrum =
        Spectrum(kl, fetchSpeedDirSpread.x, fetchSpeedDirSpread.y, theta,
                 fetchSpeedDirSpread.z, fetchSpeedDirSpread.w) *
        sqrt(timeScale.y);
    if (kl < domainMinMax.y || kl > domainMinMax.z)
    {
        spectrum = 0.0f;
    }

    vec2 rand = vec2(GaussianHash(k, 0.0f), GaussianHash(k, 1.0f));
    vec2 h0 = rand / sqrt(2) * sqrt(2 * spectrum * deltaK * deltaK);

    float phase = WaveDispersion(kl) * timeScale.x;
    vec2 ePhase = vec2(sin(phase), cos(phase));

    vec2 h = MultiplyComplex(h0, ePhase);
    vec2 dx = k.x * vec2(-h.y, h.x); // multiply complex by i unwrapped
    vec2 dy = k.y * vec2(-h.y, h.x);
    vec2 dx2 = -k.x * k.x * h; // multiply complex by i^2 unwrapped
    vec2 dxdy = -k.x * k.y * h;
    vec2 dy2 = -k.y * k.y * h;
    vec2 displacement = vec2(dx.x, dy.x) / kl;
    vec2 dxDisplacement = vec2(dx2.x, dxdy.x) / kl;
    vec2 dyDisplacement = vec2(dxdy.x, dy2.x) / kl;

    Data data = Data(h, displacement, dx, dy, dxDisplacement, dyDisplacement);
    FLY_ACCESS_STORAGE_BUFFER(Data, gPushConstants.dataBufferIndex)
    [base + c] = data;
}
