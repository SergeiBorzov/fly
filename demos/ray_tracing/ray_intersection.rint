#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : require

layout(push_constant) uniform PushConstants
{
    uint cameraBufferIndex;
    uint sphereBufferIndex;
    uint accStructureIndex;
    uint outputTextureIndex;
    uint sbtStride;
}
gPushConstants;

struct SphereData
{
    vec3 center;
    float radius;
    vec3 albedo;
    float reflectionCoeff;
};

layout(set = 0, binding = 1, std430) readonly buffer Spheres
{
    SphereData items[];
}
gSpheres[];

float SafeSqrt(float x) { return sqrt(max(0.0f, x)); }

float RaySphereIntersect(vec3 o, vec3 d, vec3 p, float r)
{
    float b = dot(o - p, d);
    float c = dot(o - p, o - p) - r * r;

    float disc = b * b - c;
    if (disc < 0.0f)
    {
        return -1.0f;
    }

    float t = (-b - SafeSqrt(disc));
    if (t < 0.0f)
    {
        t = (-b + SafeSqrt(disc));
    }
    return t;
}

void main()
{
    SphereData sphereData = gSpheres[gPushConstants.sphereBufferIndex]
                                .items[gl_InstanceCustomIndexEXT];

    float t = RaySphereIntersect(gl_WorldRayOriginEXT, gl_WorldRayDirectionEXT,
                                 vec3(sphereData.center), sphereData.radius);
    if (t > 0.0f)
    {
        reportIntersectionEXT(t, 1);
    }
}
